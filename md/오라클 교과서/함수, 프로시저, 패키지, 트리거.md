# 함수, 프로시저, 패키지, 트리거

이전의 PL/SQL에서는 주로 익명의 PL/SQL 문을 설명하였다면 이번에는 이름이 있는 PL/SQL 문을 설명하려고 한다. 이미 이름이 있는 PL/SQL 문에 대한 개념은 설명하였고 이제는 실제 작성하는 방법과 사용하는 방법을 설명하려고 한다. 이름이 있는 대표적인 PL/SQL로는 **사용자 함수, 저장 프로시저, 패키지, 트리거** 등이 있다.



다시 사용하려면 이름이 필요하며 이름을 가지려면 PL/SQL 문을 객체로 저장해야 한다. 지금까지 배운 모든 객체는 스키마에 소속되어 사용됐다. 예를 들어 JOBS라는 테이블은 HR이라는 스키마에 포함되어 있던 것처럼 명명된 PL/SQL인 저장 프로시저, 사용자 함수 역시 마찬가지로 특정 스키마에 소속되어야 하며 해당 객체를 사용하기 위한 권한 역시 필요하다.



명명된 PL/SQL 문 중 **함수와 저장 프로시저**는 하나 이상의 매개 변수를 포함하여 해당 매개 변수를 통하여 값이 처리되도록 하는 기능을 가진 PL/SQL이며, **패키지**는 헤더와 보디로 여러 개의 사용자 함수나 프로시저를 관리하는 것이며, **트리거**는 테이블로의 데이터 입력, 수정, 삭제 등의 이벤트를 처리할 때에 사용한다.





## 1. 저장 프로시저와 사용자 함수

**저장 프로시저**(Stored Procedure)와 **사용자 함수**는 오라클의 대표적인 서브 프로그램으로 분류되며 데이터 처리 작업을 수행하도록 프로그래밍할 수 있으며 두 가지 모두 매개 변수를 통해 일반화한 프로그래밍 방법을 지원한다. 이와 함께 같은 기능의 익명 PL/SQL 문보다 컴파일을 위한 추가 시간이 필요 없으므로 빠르게 실행할 수 있다.



저장 프로시저와 사용자 함수는 매우 유사하며 관련된 시스템 권한도 같다. 저장 프로시저는 절차적인 프로그램을 주로 처리하고 사용자 함수는 입력된 내용으로 특정 계산을 한 다음 그 결과를 반환한다는 데 차이가 있을 뿐 형식이나 내용은 비슷하다.



### 저장 프로시저와 사용자 함수 개념



**관련된 시스템 권한**

모든 오라클 객체의 생성이나 수정에는 시스템 권한이 필요하다.

(중략)



**객체별 권한 부여와 삭제**

오라클에서 보안을 위해 시스템 권한 이외에 객체에 실행 권한을 부여할 수 있다. 이때 사용하는 방법이 GRANT이다. 프로시저 또는 함수에서는 EXECUTE에 대한 권한 부여가 가능하며 자세한 내용은 권한 부분을 참고하기 바란다. 이러한 객체별 부분은 AUTHID 절과 연관 관계가 있다.

(중략)





**작성 이점**

저장 프로시저나 사용자 함수를 사용하게 되면 다음과 같은 이점들이 있다.



- 보안성
- 성능
- 생산성
- 무결성

(중략)



**작성 지침**

(생략)



**매개 변수**

함수와 프로시저는 매개 변수를 통하여 프로그램을 어디에든 사용할 수 있게 일반화한다. 매개변수는 프로시저나 함수를 사용할 때에 같이 사용하는 변수로, 입력된 값이나 처리하고 반환된 값을 의미한다. 이러한 매개 변수는 모드에 맞게 사용되어야 하는데, 모드는 아래의 표와 같다.



| 모드       | 설명                                                         |
| ---------- | ------------------------------------------------------------ |
| **IN**     | 읽기 전용의 매개 변수의 유형이며 모드 생략 시 IN 모드로 지정<br />프로시저나 함수를 실행할 때 값을 입력하며 해당 프로그램에 그 값이 전달된다. |
| **OUT**    | 쓰기 전용의 매개 변수의 유형이며 사용 시 OUT 모드를 지정해야 한다.<br />프로시저나 함수에서 처리되어 넘겨진 값이 전달되는 매개 변수로 사용할 때는 다른 변수가 해당 위치에 있어야 한다. |
| **IN OUT** | 읽기 쓰기가 모두 가능한 매개 변수의 유형 IN과 OUT의 기능을 모두 수행한다. |



- IN 모드

  프로시저나 함수로 값이 전달되게 하는 모드로, 기본이 되는 모드. 프로그램을 할 때 가장 기본이 되는 모드이며 가장 일반적으로 사용하는 매개 변수이다.



- OUT 모드

  IN 모드 매개 변수처럼 직접적인 값을 입력하는 것이 아닌 변수를 사용하여 프로시저나 함수에서 반환해주는 값을 전달받게 된다. 즉 함수나 프로시저에 값을 전달하는 것이 아니므로 초기에는 NULL 값으로 지정되어 있다가 프로시저나 함수에서 해당 매개 변수에 값을 전달하고 프로시저나 함수의 호출이 완료되면 값이 반환된다. 주로 다른 프로시저 안에서 서브 프로그램 형태로 많이 사용된다.



- IN OUT 모드

  IN 모드와 OUT 모드가 결합된 형식이다. 실제 매개 변수의 값이 프로시저나 함수에 전달되고 프로그램이 실행이 완료되면 다시 해당 변수로 값이 반환된다. OUT 모드와 마찬가지로 다른 프로시저 안에서 서브 프로그램 형태로 많이 사용된다. 프로그램의 가독성이나 이를 이해하는데 도움이 안 되므로 사용을 권하지 않는 모드이다.



이러한 매개 변수는 프로시저 또는 함수의 헤더 부분에서 지정하게 되는데, 매개 변수 모드 데이터 유형의 형식으로 지정한다. 데이터의 타입은 문자형, 숫자형, 일자형, 객체형으로 지정할 수 있는데, 제약사항을 사용할 수는 없다.



```plsql
/* 잘못된 매개 변수 지정 예 */
CREATE OR REPLACE PROCEDURE TEST (P_PARAM IN NUMBER(2), ...)

/* 정확한 매개 변수 지정 예 */
CREATE OR REPLACE PROCEDURE TEST (P_PARAM IN NUMBER, ...)
```





### 저장 프로시저

저장 프로시저는 하나 또는 그 이상의 작업을 수행하는 모듈이다. 이러한 저장 프로시저는 PL/SQL에서 단독으로 실행할 수 있게 구성된다. 이러한 모듈로 만들어진 프로시저는 단독으로 사용되기도 하고 프로그램 로직으로 처리되어 다른 프로시저나 PL/SQL에서 재사용하기 도 한다.



```plsql
CREATE [OR REPLACE] PROCEDURE [스키마.]프로시저명 [(파라미터 1 [, 파라미터2, ...]) ]
{IS | AS} 
	변수 선언부
BEGIN
	실행부
[EXCEPTION
	예외 처리부]
END [프로시저명];
```



CREATE OR REPLACE 문장을 사용하여 생성과 수정을 동시에 처리한다. 또한, 스키마를 지정하여 만들 수 있으며 파라미터는 파라미터 이름, 모드, 데이터 유형으로 구성된다. 모드는 IN, OUT, IN OUT이 있으며 생략되면 IN으로 인식한다. 또한, **변수 선언부**는 프로시저 안에서 사용할 변수를 선언하는 부분이며 **실행부**는 실제 동작이 일어나는 부분이고 **예외 처리부**는 예외 상황이 발생하였을 때 이를 처리하는 부분이다. 마지막으로 END 문 뒤에 프로시저 이름을 명시한다.





**단순 프로시저**

```plsql
DECLARE
	V_NAME VARCHAR2(30) := '';
BEGIN
	V_NAME := 'LEE';
	DBMS_OUTPUT.PUT_LINE('MY NAME IS ' || V_NAME);
END;
/
```



위의 익명 PL/SQL을 TEST_PROC이라는 이름의 프로시저로 만들어 보면,

```plsql
CREATE OR REPLACE PROCEDURE TEST_PROC
IS
	V_NAME VARCHAR2(30) := '';
BEGIN
	V_NAME := 'LEE';
	DBMS_OUTPUT.PUT_LINE('MY NAME IS ' || V_NAME);
END;
/

EXEC TEST_PROC;
```





**IN 파라미터 프로시저**

파라미터가 없는 단순 프로시저를 만들어 보았는데, 단순 프로시저는 앞서 본 익명 PL/SQL 문처럼 고정적으로만 동작이 된다. 이러한 내용을 보완할 수 있는 장치가 파라미터이다. 우선 가장 기본이 되는 IN 모드만 있는 프로시저를 살펴보도록 하자.



```plsql
CREATE OR REPLACE PROCEDURE TEST_PROC (P_NAME VARCHAR)
IS
BEGIN
	DBMS_OUTPUT.PUT_LINE('MY NAME IS ' || P_NAME);
END;
/

EXEC TEST_PROC('홍길동');
```



**OUT 파라미터 프로시저**

OUT 파라미터 프로시저는 IN 파라미터 프로시저와는 사용 방법이 다르다. OUT 파라미터 프로시저는 다른 프로시저에서 호출되어 사용되는 프로시저라고 생각하면 이해하기 쉽다. 우선 OUT 파라미터 프로시저를 만들자.



```plsql
CREATE OR REPLACE PROCEDURE TEST_PROC (P_NAME OUT VARCHAR2)
IS
BEGIN
	P_NAME := 'MY NAME IS LEE';
END;
/
```



OUT 파라미터 프로시저의 실행은 EXECUTE나 EXEC를 이용하여 실행하는 것이 아니다. 실질적으로 사용을 확인하려면 PL/SQL 문을 작성해서 확인해야 한다.

```plsql
DECLARE
	V_NAME VARCHAR2(100) := '';
BEGIN 
	TEST_PROC(V_NAME);
	DBMS_OUTPUT.PUT_LINE(V_NAME);
END;
/
```





**IN OUT 파라미터 프로시저**

이번에는 IN OUT 파라미터를 사용하는 방법을 알아보자. OUT 파라미터 프로시저처럼 사용해야 하지만 입력용으로 사용한다는 점이 다르다. IN OUT을 사용하지 않으면 IN과 OUT 파라미터를 사용하는 것과 같다.



```plsql
CREATE OR REPLACE PROCEDURE TEST_PROC (P_NAME IN OUT VARCHAR2)
IS 
BEGIN
	P_NAME := 'MY NAME IS ' || P_NAME;
END;
/


DECLARE
	V_NAME VARCHAR2(100) := '';
BEGIN
	V_NAME := '홍길동';
	TEST_PROC(V_NAME);
	DBMS_OUTPUT.PUT_LINE(V_NAME);
END;
/
```



**복합 파라미터 프로시저**

실제 프로시저에는 여러 가지 파라미터를 복합적으로 사용한다. 위의 IN OUT 파라미터 프로시저의 예를 IN 파라미터와 OUT 파라미터로 바꾸어 같은 기능을 만들 수 있다.

```plsql
CREATE OR REPLACE PROCEDURE TEST_PROC (P_NAME IN VARCHAR2, P_MSG OUT VARCHAR2)
IS 
BEGIN
	P_MSG := 'MY NAME IS ' || P_NAME;
END;
/

DECLARE 
	V_NAME VARCHAR2(100) := '';
BEGIN
	TEST_PROC('홍길동', V_NAME);
	DBMS_OUTPUT.PUT_LINE(V_NAME);
END;
/
```





**저장 프로시저 삭제**

```plsql
DROP PROCEDURE [스키마.]프로시저명;
```





**저장 프로시저 실행**

저장 프로시저를 실행할 때는 EXECUTE 또는 EXEC를 이용하여 실행한다. 



```plsql
CREATE OR REPLACE PROCEDURE TEST_PROC (P_DAN NUMBER, P_MAX_NUM NUMBER)
IS
	V_CNT NUMBER;
BEGIN
	FOR V_CNT IN 1 .. P_MAX_NUM
	LOOP
		DBMS_OUTPUT.PUT_LINE(TO_CHAR(P_DAN) || ' * ' || TO_CHAR(V_CNT)
				|| ' = ' || TO_CHAR(P_DAN * V_CNT));
	END LOOP;
END;
/

EXEC TEST_PROC (3, 9);
```





- RETURN

  OUT 모드 프로시저를 다른 PL/SQL 문에서 사용하는 방법을 확인해보았으므로 이제는 PL/SQL 문이 아닌 반환된 내용을 처리하는 방법을 알아보도록 하자.



```plsql
CREATE OR REPLACE PROCEDURE TEST_PROC (P_NAME OUT VARCHAR2)
IS
BEGIN
	P_NAME := 'MY NAME IS LEE';
END;
/

VAR V_NAME VARCHAR2;
EXEC TEST_PROC(:V_NAME);
PRINT V_NAME;
```



OUT 모드나 IN OUT 모드에서 사용되려면 변수를 먼저 지정하고 프로시저 실행을 통해서 반환값을 전달받고 실행하면 된다. 그런데 이전의 OUT이나 IN OUT 프로시저를 실행할 때와는 다른 것을 확인할 수 있다. 이전의 PL/SQL 문 안에서 사용될 때는 EXECUTE 또는 EXEC 명령을 사용하지 않았지만, 이 예제에서는 EXEC를 통해서 실행한 것을 확인할 수 있다. 즉, 다른 프로시저나 PL/SQL 문 안에서 실행할 때는 실행 명령을 사용하지 않지만, 독자적으로 수행할 때는 실행 명령을 이용하여 VAR을 통해 변수를 선언하고 사용할 때는 반드시 쌍점(:)을 붙여야 한다.





### 사용자 함수

사용자 함수(Function)는 오라클에서 기본적으로 제공하는 함수 이외에 사용자가 필요에 따라 만든 것을 사용자 함수라고 한다. 즉, 사용 방법과 기능이 모두 기존 함수와 비슷하다고 생각하면 쉽게 이해할 수 있다. 또한, 프로시저와 성격이 매우 비슷하지만 반환값이 있느냐 없느냐가 가장 큰 차이라고 할 수 있다. 



**(1) 사용자 함수 생성**

```plsql
CREATE [OR REPLACE] FUNCTION [스키마.]함수 이름 [(파라미터1 [, 파라미터2, ...]) ]
RETURN 반환 데이터 타입
{IS | AS}
	변수 선언부
BEGIN
	실행부
	RETURN 반환값;
[EXCEPTION 
	예외처리부]
END;
```





```plsql
CREATE OR REPLACE FUNCTION TEST_FUNC (P_NAME IN VARCHAR2)
RETURN VARCHAR2
IS
	V_NAME VARCHAR2(100) := '';
BEGIN	
	V_NAME := 'MY NAME IS ' || P_NAME;
	RETURN V_NAME;
END;
/
```





**(2) 사용자 함수 실행**

사용자 함수의 실행 방법은 일반 함수와 매우 유사하다. SELECT 문 같은 SQL 문에서도 사용할 수 있으며, PL/SQL 문에서 변수를 통하는 방법으로도 실행할 수 있다. 단 변수를 통한 방법은 데이터의 타입에 주의하여 사용하여야 한다.



**[주의] ** 사용자 함수를 실행할 때는 다음 사항에 주의한다.

- SELECT 문에서 사용되는 함수에는 DML을 포함해서는 안 된다.
- UPDATE나 DELETE 문에서 사용되는 함수에는 같은 테이블에 대한 DML과 SELECT 문이 포함돼서는 안 된다.



다음은 SQL 문에서 사용자 함수를 실행한 모습이다.

```plsql
SELECT TEST_FUNC('홍길동') FROM DUAL;
```



다음은 변수를 이용하여 사용자 함수를 실행한 모습이다.

```plsql
VAR V_NAME VARCHAR2
EXEC :V_NAME := TEST_FUNC('홍길동');
print V_NAME;
```



다음은 PL/SQL 문에서 사용자 함수를 실행한 모습이다.

```plsql
DECLARE
	V_NAME VARCHAR2(100) := '';
BEGIN
	V_NAME := TEST_FUNC('홍길동');
	DBMS_OUTPUT.PUT_LINE(V_NAME);
END;
/
```



**(3) 매개 변수를 이용한 사용자 함수 실행**

(생략)





## 2. 패키지



(생략)





## 3. 트리거

트리거는 프로시저와 매우 비슷하나 실행시키는 이벤트가 테이블과 관계되어 있어 수동으로 실행할 수는 없고 테이블과 관련된 이벤트로만 실행할 수 있다. 이해하기 쉽도록 예를 들면 테이블과 관계된 이벤트는 INSERT, UPDATE, DELETE로 정해져 있으며, 이때 관련된 매개 변수 역시 테이블에서 사용되는 변화된 값으로, 테이블 칼럼의 이전 값과 새로운 값으로 제한된다.





### 트리거 개념

트리거는 DB 이벤트에 반응하여 실행되는 프로그램 단위이다. 트리거의 주요 사용 목적은 **테이블 데이터의 무결성 보장, DB 관리의 자동화** 등이라고 할 수 있다. 주로 첫 번째는 개발자들과 관련된 DML 트리거가 대부분이며, 두 번째는 주로 DBA가 DDL 트리거나 DB 실행이나 종료 이벤트를 이용한 초기화와 정리 작업 트리거가 여기에 속한다.



**DML 트리거**

DML 트리거는 DML 문(INSERT, UPDATE, DELETE)이 테이블의 하나 이상의 데이터에 영향을 미칠 때에 자동으로 실행된다. 프로시저나 함수는 사용하려면 직접 호출해야 하지만, 트리거는 오라클에서 사용자에게 알려주지 않고 호출하여 처리한다. 따라서, 사용자는 트리거가 있다면 해당 내용이 언제 어떻게 실행되는지를 파악하는 것이 매우 중요하다.



이러한 트리거를 작성할 때 기본적으로 트리거의 행동에 의해 참조되는 모든 객체에 대한 적절한 권한이 필요하다. 예를 들어 트리거에서 특정 테이블에 데이터를 INSERT하려고 한다면 테이블에 INSERT할 권한이 있어야 한다.



**DML 트리거 특징**

- 자동으로 실행되며 수동으로는 실행할 수 없다.
- 어떤 동작에서 실행되는지 지정해야 한다.
- 작동이 일어난 트리거 대상 테이블에 대해서는 트리거 내용이 존재할 수 없다.
- BEFORE(INSTEAD OF), AFTER 두 가지 트리거가 있다.
- 트랜잭션 일부로 처리된다.



더 쉽게 이야기하자면 트리거는 프로시저나 함수와 마찬가지로 선언, 실행, 예외 처리가 있는 PL/SQL 블록으로 구성되지만, 독립적으로 실행되는 DB 개체로, DB의 실행과 관련된 선언부가 필요하다는 것도 알아야 한다. 

DML 트리거는 트랜잭션 일부로 처리되기 때문에 DML 트리거 내에서는 COMMIT, ROLLBACK, SAVEPOINT와 같은 문장을 포함할 수 없으며(물론 독립적 트랜잭션을 사용하는 것은 제외) 트리거에서 예외가 발생하면 트랜잭션은 롤백이 된다.





**트리거 관련 용어**

- **BEFORE 트리거**: 특정 동작 이전에 작동해야 하는 트리거로, BEFORE INSERT같은 트리거가 여기에 속한다.
- **AFTER 트리거**: 특정 동작 이후에 작동해야 하는 트리거로, AFTER UPDATE 같은 트리거가 여기에 속한다.
- **문장 레벨 트리거**: 작성된 DML 문장 전체에 대해서 작동하는 트리거로, 반대 개념이 행 레벨 트리거이다.
- **행 레벨 트리거**: 작성된 DML 문장에 대해서 행마다 동작하는 트리거이다. 만일 TEST 테이블에 100개의 데이터가 있고 UPDATE 트리거가 있다면 UPDATE TEST SET COL01 = 'N'이라고 실행하면 트리거는 100회 호출되는 개념이다. 트리거의 동작이 간단하고 이해하기 쉽지만 호출되는 구조에 의해 성능에 영향을 미칠 수 있는 트리거이다.
- **NEW 레코드**: 트리거의 NEW 레코드는 PL/SQL의 레코드와 유사하며 :NEW로 사용하면 된다. NEW 의 레코드는 입력과 수정에서 사용할 수 있다.
- **OLD 레코드**: 트리거의 OLD 레코드는 NEW 레코드와 반대의 개념으로 :OLD로 사용하며 수정과 삭제에서 사용할 수 있다.
- **WHEN 절**: 불필요한 트리거의 실행을 막고자 DML 트리거의 실행 여부를 결정하는 부분이다.





**DDL 트리거**

DDL 트리거는 말 그대로 DDL 문 실행 시 작동하는 트리거를 말한다. 예를 들면 테이블 또는 인덱스 같은 DB 객체를 생성하거나 수정하는 문장이 실행되면 트리거가 동작한다. 대표적인 문장으로 CREATE, ALTER, DROP 등이 있으며 객체를 생성, 수정, 제거한다. DML 트리거와 문법은 거의 같지만 트리거 작동 이벤트와 테이블 단위로 동작하는 것이 아니므로 테이블 대신 DB나 스키마로 지정되는 것이 다르다.





### DML 트리거 생성과 수정

**(1) DML 트리거 생성**

```plsql
CREATE [OR REPLACE] TRIGGER <트리거 이름>
{BEFORE | AFTER} <이벤트> ON <테이블>
[FOR EACH ROW]
[WHEN (<조건>)]
[DECLARE <변수 선언>]
BEGIN
	<실행 코드>
[EXCEPTION <예외 사항>]
END <트리거 이름>;
```



테이블에 이벤트가 발생하는 DML 문이 실행되면 해당 이벤트 전에 동작을 전체 문장 단위로 적용되게 할건지 아니면 각각의 행별로 적용되게 할건지 결정하고 해당 동작에 맞게 트리거를 생성한다.



- **{BEFORE | AFTER}**: BEFORE 트리거를 만들 것인지 AFTER 트리거를 만들 것인지를 결정한다. 예를 들어 INSERT의 경우 등록 시간 칼럼에 값을 현재 시스템의 시간으로 바꾸어 주려 한다면 BEFORE INSERT로 사용해야 한다.
- **<이벤트> ON <테이블>**: INSERT, UPDATE, DELETE, UPDATE OF 칼럼목록을 하나 또는 OR로 연결하여 여러 개를 사용할 수 있다. 예를 들어 INSERT ON TEST_TABLE 또는 INSERT OR DELETE OR UPDATE ON TEST_TABLE로 사용할 수 있다는 뜻이다.
- **\[FOR EACHROW]**: 생략하면 문장 레벨의 트리거이고 FOR EACHROW를 사용하면 행 레벨 트리거로 동작한다. FOR EACHROW 문장 앞에 REFERENCING NEW AS New OLD AS Old를 선언하여 NEW 레코드와 OLD 레코드의 별명을 부여할 수 있다.
- **[WHEN (<조건>)]**: 조건이 성립할 때만 트리거가 동작한다. \<이벤트> 중에서 UPDATE OF 칼럼 목록과 혼동하지 말아야 한다. UPDATE OF 칼럼 목록은 변경되는 칼럼에 대한 조건이고 WHEN에서 이야기하는 조건은 칼럼 값에 대한 조건이다.
- **[DECLARE <변수 선언>]**: 계산 또는 임시 저장을 위한 지역 변수가 필요할 때에 사용하는 부분이다. NEW 또는 OLD는 직접 선언해서는 안된다.
- **BEGIN ~ END**: 트리거가 실제 수행하는 내용을 코딩하는 부분이다. 만일 예외 상황이 발생하였다면 EXCEPTION 부분을 만들어서 처리해줄 수 있다.



```plsql
CREATE OR REPLACE TRIGGER TR_USER_TEST
BEFORE INSERT OR UPDATE OR DELETE ON TB_USER_INFO
FOR EACH ROW 
	DECLARE V_DIV VARCHAR2(1);
	BEGIN 
	IF INSERTING THEN
		INSERT INTO TB_USER_HISTORY (CHG_DIV, CHG_DTTM, USER_ID, USER_NAME)
		VALUES ('I', SYSDATE, :NEW.USER_ID, :NEW.USER_NAME);
	ELSIF DELETING THEN
		INSERT INTO TB_USER_HISTORY (CHG_DIV, CHG_DTTM, USER_ID, USER_NAME)
		VALUES ('D', SYSDATE, :OLD.USER_ID, :OLD.USER_NAME);
	ELSIF UPDATING THEN
    	INSERT INTO TB_USER_HISTORY (CHG_DIV, CHG_DTTM, USER_ID, USER_NAME)
		VALUES ('U', SYSDATE, :NEW.USER_ID, :NEW.USER_NAME);
	END IF;
END TR_USER_TEST;
```





**문장 레벨 트리거**

문장 레벨 트리거는 DML 문장당 한 번만 실행되기 때문에 데이터의 정합성 처리보다는 실행 로그를 남기는 경우가 대부분이다. 문장 레벨 트리거는 FOR EACH ROW 문이 생략된다.



UPDATE 문이 실행되면 현재 시각에 문장이 수정되었다고 하는 내용을 보여주는 트리거를 TB_PKG_TEST 테이블에 적용시키는 예제

```plsql
CREATE OR REPLACE TRIGGER TR_PKG_TEST_UPDATE
BEFORE UPDATE ON TB_PKG_TEST
BEGIN
	DBMS_OUTPUT.PUT_LINE('현재시간 ' || TO_CHAR(SYSDATE, 'YYYY-MM-DD HH24:MI:SS') 
                         		   || ' TB_PKG_TEST가 수정되었습니다.');
END;
/
```





**행 레벨 트리거**

행 레벨 트리거는 DML 문에 의해 변경이 일어난 각각의 행별로 트리거가 동작하기 때문에 데이터의 정합성 보장에 좋고 개발자들이 쉽게 이해할 수 있는 트리거이다. 또한, 행별로 OLD와 NEW 레코드가 있으며 이러한 내용을 기반으로 트리거를 작성한다. 실질적으로 OLD 또는 NEW 레코드는 트리거에서 사용되는 테이블의 ROWTYPE과 마찬가지로 사용할 수 있다.



각각의 DML에 따라 NEW와 OLD의 값이 달라지는데, 자세한 내용은 아래 표를 참고하자.



| DML    | OLD                                       | NEW                                         |
| ------ | ----------------------------------------- | ------------------------------------------- |
| INSERT | 값이 지정되지 않고 NULL 값을 가진다.      | 테이블에 입력되는 값으로 NEW 값이 적용된다. |
| UPDATE | 변경되기 이전의 값으로 OLD 값이 적용된다. | 수정되는 값으로 NEW 값이 적용된다.          |
| DELETE | 삭제되기 이전의 값으로 OLD 값이 적용된다. | 값이 지정되지 않고 NULL 값을 가진다.        |





이번에는 TB_USER_INFO라는 사용자 테이블에 입력, 수정, 삭제가 일어나면 그 이력을 TB_USER_HISTORY라는 테이블에 남기는 트리거를 작성하여 보자.



1) 테이블 생성

```sql
CREATE TABLE TB_USER_INFO (
	USER_ID     VARCHAR2(50),
	USER_NAME   VARCHAR2(50),
	USER_NICK   VARCHAR2(50),
	USER_MOBILE VARCHAR2(20),
	CONSTRAINT PL_USER_INFO PRIMARY KEY (USER_ID)
);

CREATE TABLE TB_USER_HISTORY (
	USER_ID         VARCHAR2(50),
    CHG_DTTM        DATE,
    CHG_DIV         VARCHAR2(10),
    OLD_USER_NAME   VARCHAR2(50),
    NEW_USER_NAME   VARCHAR2(50),
    OLD_USER_NICK   VARCHAR2(50),
    NEW_USER_NICK   VARCHAR2(50),
    OLD_USER_MOBILE VARCHAR2(20),
    NEW_USER_MOBILE VARCHAR2(20),
    CONSTRAINT PK_USER_HISTORY PRIMARY KEY (USER_ID, CHG_DTTM)
);
```



2) TB_USER_INFO에 입력, 수정, 삭제 시 TB_USER_HISTORY에 이력을 남기는 트리거를 작성하여 보자.

```plsql
CREATE OR REPLACE TRIGGER TR_USER_INFO_ALL
BEFORE INSERT OR UPDATE OR DELETE ON TB_USER_INFO
REFERENCING NEW AS New OLD AS Old FOR EACH ROW
BEGIN
	IF INSERTING THEN
		INSERT INTO TB_USER_HISTORY (USER_ID, CHG_DTTM, 
           CHG_DIV, OLD_USER_NAME, NEW_USER_NAME,
           OLD_USER_NICK, NEW_USER_NICK, OLD_USER_MOBILE,
           NEW_USER_MOBILE)
        VALUES (:New.USER_ID, SYSDATE, 'I', '', :New.USER_NAME, '',
                :New.USER_NICK, '', :New.USER_MOBILE);
    ELSIF DELETING THEN
		INSERT INTO TB_USER_HISTORY (USER_ID, CHG_DTTM, 
           CHG_DIV, OLD_USER_NAME, NEW_USER_NAME,
           OLD_USER_NICK, NEW_USER_NICK, OLD_USER_MOBILE,
           NEW_USER_MOBILE)
        VALUES (:Old.USER_ID, SYSDATE, 'D', 
                :Old.USER_NAME, '', :Old.USER_NICK, '',
                :Old.USER_MOBILE, '');
    ELSIF UPDATING THEN
		INSERT INTO TB_USER_HISTORY (USER_ID, CHG_DTTM, 
           CHG_DIV, OLD_USER_NAME, NEW_USER_NAME,
           OLD_USER_NICK, NEW_USER_NICK, OLD_USER_MOBILE,
           NEW_USER_MOBILE)
        VALUES (:New.USER_ID, SYSDATE, 'U', :Old.USER_NAME, 
                :New.USER_NAME, :Old.USER_NICK, :New.USER_NICK, 
                :Old.USER_MOBILE, :New.USER_MOBILE);
    END IF;
END TR_USER_INFO_ALL;
/
```



3) TB_USER_INFO에 자료를 입력, 수정, 삭제해보고 TB_USER_HISTORY 테이블의 데이터를 확인하자.

```sql
INSERT INTO TB_USER_INFO (USER_ID, USER_NAME, USER_NICK, USER_MOBILE)
VALUES ('TEST_USER6', '한대수', '한대수', '010-2222-3333');

UPDATE TB_USER_INFO SET USER_NICK = USER_NAME;

DELETE TB_USER_INFO WHERE USER_NAME = '한대수';


SELECT * FROM TB_USER_HISTORY;
```





**(2) 트리거 활성화, 비활성화**

트리거는 삭제하지 않고 활성화하거나 비활성화할 수 있다. 트리거의 활성화 또는 비활성화의 범위 지정은 트리거 단위 또는 테이블 단위로 지정할 수 있다.

```plsql
ALTER TRIGGER <트리거 이름> {DISABLE | ENABLE};
ALTER TABLE <테이블 이름> {DISABLE | ENABLE} ALL TRIGGERS;
```



**(3) 트리거 삭제**

트리거를 삭제하려면 DROP 문을 이용한다. 테이블을 삭제하면 관련된 모든 트리거도 함께 삭제된다.

```plsql
DROP TRIGGER <트리거 명>;
```